apply plugin: 'com.android.application'

// When no project property is set for Gradle ('-PJSEngine=')
// default JS engine is set to v8android
// JSEngine property can be:
// - jsc for JavaScriptCore
// - v8android for V8

def jsEngineBaseDir = "${buildDir}/JS"
def jsEngineIncDir = "${jsEngineBaseDir}/include/V8"
def jsEngineLibDir = "${rootDir}/app/src/main/jniLibs"

def jsEngineLibName = "v8android"
if (project.hasProperty("JSEngine")) {
    jsEngineLibName = project.property("JSEngine")
    if ("${jsEngineLibName}" == "jsc" ) {
        jsEngineIncDir = "${jsEngineBaseDir}/include"
    }
}

def graphics_api = "OpenGL"
def platformVersion = 26
if (project.hasProperty("GRAPHICS_API")) {
    graphics_api = project.property("GRAPHICS_API")
    if (graphics_api == "Vulkan" ) {
        platformVersion = 26 // minimal version to get Vulkan
    }
}

def unity_build = "false"
if (project.hasProperty("UNITY_BUILD")) {
    unity_build = project.property("UNITY_BUILD")
}
def arcore_libpath = "${buildDir}/arcore-native"

configurations { natives }

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.android.babylonnative.playground"
        minSdk platformVersion
        targetSdkVersion 33
        ndkVersion "21.4.7075529"
        if (project.hasProperty("NDK_VERSION")) {
            def NDKVersion = project.property("NDK_VERSION")
            ndkVersion  "${NDK_VERSION}"
        }
        externalNativeBuild {
            cmake {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
                arguments "-DANDROID_STL=c++_shared",
                    "-DENABLE_PCH=OFF",
                    "-DGRAPHICS_API=${graphics_api}",
                    "-DARCORE_LIBPATH=${arcore_libpath}/jni",
                    "-DANDROID_JSENGINE_LIBPATH=${jsEngineLibDir}",
                    "-DANDROID_JSENGINE_LIBNAME=${jsEngineLibName}",
                    "-DANDROID_JSENGINE_INCPATH=${jsEngineIncDir}",
                    "-DCMAKE_UNITY_BUILD=${unity_build}"
            }
        }
        ndk {
            if (project.hasProperty("ARM64Only")) {
                abiFilters "arm64-v8a"
            } else {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86"
            }
        }
    }

    buildTypes {
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    externalNativeBuild {
        cmake {
            version '3.19.6'
            path 'CMakeLists.txt'
        }
    }

    packagingOptions {
        pickFirst 'lib/*/libv8android.so'
        pickFirst 'lib/*/libjsc.so'
    }
    buildFeatures.prefab = true
}

dependencies {

    implementation 'com.android.ndk.thirdparty:openssl:1.1.1q-beta-1'

    //Activity
    implementation "androidx.activity:activity:1.6.1"

    // ARCore library
    implementation 'com.google.ar:core:1.35.0'
    natives 'com.google.ar:core:1.35.0'

    //implementation 'com.android.support:appcompat-v7:27.1.1'
    //implementation 'com.android.support:design:27.1.1'
    // Apps must declare play-services-location version >= 16.
    // In the following line, substitute `16 (or later)` with the latest version.
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    // Apps must declare play-services-auth version >= 16.
    // In the following line, substitute `16 (or later)` with the latest version.
    implementation 'com.google.android.gms:play-services-auth:20.4.0'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibraries() {
    // Always extract, this insures the native libs are updated if the version changes.
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into arcore_libpath
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}
task copyAssets(type: Copy) {
    from '../../../../TownBuildersJs/public/assets/.'
    into 'src/main/assets/assets/'
}


task copyFiles {
    copy
    {
        from '../../../node_modules/babylonjs'
        include "babylon.max.js"
        into 'src/main/assets/Scripts'
    }
    copy
    {
        from '../../../node_modules/babylonjs-loaders'
        include "babylonjs.loaders.js"
        into 'src/main/assets/Scripts'
    }
    copy
    {
        from '../../../node_modules/babylonjs-materials'
        include "babylonjs.materials.js"
        into 'src/main/assets/Scripts'
    }
    copy
    {
        from '../../../node_modules/babylonjs-gui'
        include "babylon.gui.js"
        into 'src/main/assets/Scripts'
    }
    copy
    {
        from '../../../Dependencies'
        include "*.js"
        into 'src/main/assets/Scripts'
    }
    copy
            {
                from '../../Scripts'
                include "*.js"
                into 'src/main/assets/Scripts'
            }
    copy
            {
                from '../../../../TownBuildersJs/public'
                include "index.js"
                into 'src/main/assets/Scripts'
            }
}

repositories {
    mavenLocal()
    maven {
        // Android JSC is installed from npm
        url "$rootDir/../../node_modules/jsc-android/dist"
    }
    maven {
        // Android V8 is installed from npm
        url "$rootDir/../../node_modules/v8-android/dist"
    }
    google()
    jcenter()
}

configurations 
{ 
    jscAndroid
    v8Android 
}

dependencies {
    jscAndroid 'org.webkit:android-jsc:+'
    v8Android 'org.chromium:v8-android:+'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractJSEngineLibraries() {
    // Always extract, this insures the native libs are updated if the version changes.
    outputs.upToDateWhen { false }
    def jsconfig
    if ("${jsEngineLibName}" == "jsc" ) {
        jsconfig = configurations.jscAndroid
    } else {
        jsconfig = configurations.v8Android
    }
    doFirst {
        jsconfig.files.each { f ->
            copy {
                from zipTree(f)
                into jsEngineLibDir
                include "jni/**/*"
                // remove 'jni/'
                eachFile { fcd ->
                    fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
                }
                includeEmptyDirs = false
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractJSEngineLibraries)
    }
}

task copyFilesJSHeaders {
    copy
    {
        from "${rootDir}/../../node_modules/jsc-android/dist/include"
        include "*"
        into "${jsEngineBaseDir}/include/JavaScriptCore"
    }
    copy
    {
        from "${rootDir}/../../node_modules/v8-android/dist/include"
        include "**/*"
        into "${jsEngineIncDir}"
    }
}

preBuild.dependsOn(copyFiles)
preBuild.dependsOn(copyAssets)
preBuild.dependsOn(copyFilesJSHeaders)
